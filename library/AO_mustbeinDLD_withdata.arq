#QueryScope=ImageInstance
#QueryReturnType=Graph
#QueryResults=All image instances that must be on DLD for distribution outside of China
#QueryUrl=/AO_mustbeinDLD_withdata

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?inDLDw skos:prefLabel ?resL .
    ?inDLDw tmp:isMain true .
    ?inDLDw tmp:lastSync ?sdate .
    ?inDLDw tmp:status ?status .
    ?inDLDw tmp:provider ?provider .
    ?inDLDw tmp:hasOpen ?iAccessOpen .
    ?inDLDw tmp:hasReproAccess ?access .
    ?inDLDw tmp:hasImage true .
    ?inDLDw tmp:hasEtext false .
    ?inDLDw a bdo:ImageInstance .
    ?inDLDw :instanceReproductionOf ?i .
    ?inDLDw ?ip ?io .

    # We cheat a bit and attach the topic to the instance instead
    # of the corresponding work
    ?inDLDw ?wp ?wo .
    ?wo skos:prefLabel ?wol .

    # Same for the author, entity score and language
    ?inDLDw tmp:author ?author .
    ?agent skos:prefLabel ?agentL .

    ?inDLDw tmp:thumbnailIIIFService ?th .
}
WHERE 
{
   {
      ?wadm adm:access bda:AccessOpen ;
            adm:status bda:StatusReleased ;
            adm:adminAbout ?inDLDw .
      FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?ths } || exists {?inDLDw tmp:thumbnailIIIFSelected ?ths })
      FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
      ?inDLDw :instanceReproductionOf ?mw .
      FILTER (not exists {?inDLDw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) })
      ?mw :instanceOf ?wa .
      ?wa :language bdr:LangBo .
    
      { ?inDLDw tmp:thumbnailIIIFService ?th . }
      union
      { ?inDLDw tmp:thumbnailIIIFSelected ?th . }

      ?wa skos:prefLabel ?resL .

    } union {

        ?wadm adm:access bda:AccessOpen ;
              adm:status bda:StatusReleased ;
              adm:adminAbout ?inDLDw .
        FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?ths } || exists {?inDLDw tmp:thumbnailIIIFSelected ?ths })
        FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
        ?inDLDw :instanceReproductionOf ?mw .
        FILTER (not exists {?inDLDw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) })
        ?mw :instanceOf ?wa .
        ?wa :language bdr:LangBo .

      bind( true as ?iAccessOpen)
      bind (bda:StatusReleased as ?status)
      ?inDLDw ?ip ?io .
      FILTER (?ip != bdo:note && ?ip != rdf:type && ?ip != bdo:instanceHasReproduction && ?ip != tmp:thumbnailIIIFService && ?ip != bdo:instanceEvent && ?ip != bdo:hasTitle && ?ip != bdo:hasPart
        && ?ip != bdo:colophon && ?ip != bdo:incipit)


      { ?inDLDw tmp:thumbnailIIIFService ?th . }
      union
      { ?inDLDw tmp:thumbnailIIIFSelected ?th . }

    } union {
        ?wadm adm:access bda:AccessOpen ;
              adm:status bda:StatusReleased ;
              adm:adminAbout ?inDLDw .
        FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?ths } || exists {?inDLDw tmp:thumbnailIIIFSelected ?ths })
        FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
        ?inDLDw :instanceReproductionOf ?mw .
        FILTER (not exists {?inDLDw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) })
        ?mw :instanceOf ?wa .
        ?wa :language bdr:LangBo .
      ?wa bdo:creator ?creator .
      ?creator bdo:role ?role .
      ?creator bdo:agent ?agent .
      ?agent skos:prefLabel ?agentL .
      bind( if(?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agent, 1/0) as ?author)

      { ?inDLDw tmp:thumbnailIIIFService ?th . }
      union
      { ?inDLDw tmp:thumbnailIIIFSelected ?th . }

    } union {
      ?wadm adm:access bda:AccessOpen ;
            adm:status bda:StatusReleased ;
            adm:adminAbout ?inDLDw .
      FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?ths } || exists {?inDLDw tmp:thumbnailIIIFSelected ?ths })
      FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
      ?inDLDw :instanceReproductionOf ?mw .
      FILTER (not exists {?inDLDw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) })
      ?mw :instanceOf ?wa .
      ?wa :language bdr:LangBo .
      ?wa ?wp ?wo .
      FILTER(?wp IN(bdo:workIsAbout , bdo:workGenre))
      ?wo skos:prefLabel ?wol .

      { ?inDLDw tmp:thumbnailIIIFService ?th . }
      union
      { ?inDLDw tmp:thumbnailIIIFSelected ?th . }
    }
}