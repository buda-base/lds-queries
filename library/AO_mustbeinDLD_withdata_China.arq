#QueryScope=ImageInstance
#QueryReturnType=Graph
#QueryResults=All image instances that must be on DLD for distribution outside of China
#QueryUrl=/AO_mustbeinDLD_withdata_China

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?inDLDw skos:prefLabel ?resL .
    ?inDLDw tmp:isMain true .
    ?inDLDw tmp:lastSync ?sdate .
    ?inDLDw tmp:status ?status .
    ?inDLDw tmp:provider ?provider .
    ?inDLDw tmp:hasOpen ?iAccessOpen .
    ?inDLDw tmp:hasReproAccess ?access .
    ?inDLDw tmp:hasImage true .
    ?inDLDw tmp:hasEtext false .
    ?inDLDw a bdo:ImageInstance .
    ?inDLDw :instanceReproductionOf ?i .
    ?inDLDw ?ip ?io .

    # We cheat a bit and attach the topic to the instance instead
    # of the corresponding work
    ?inDLDw ?wp ?wo .
    ?wo skos:prefLabel ?wol .

    # Same for the author, entity score and language
    ?inDLDw tmp:author ?author .
    ?agent skos:prefLabel ?agentL .

    ?inDLDw tmp:thumbnailIIIFService ?th .
}
WHERE 
{
   {
      ?wadm adm:access bda:AccessOpen ;
            adm:status bda:StatusReleased ;
            adm:adminAbout ?inDLDw .
      FILTER (not exists {?wadm adm:restrictedInChina true })
      FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?th})
      FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
      ?inDLDw :instanceReproductionOf ?mw .
      FILTER (not exists {?mw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) } || exists {?mw :printMethod ?pm FILTER(?pm = bdr:PrintMethod_Manuscript || ?pm = bdr:PrintMethod_Relief_WoodBlock)})
      FILTER (not exists {?inDLDw :instanceHasVolume ?v . ?v :hasIIIFManifest ?iiif })
      FILTER (exists {?mw :instanceOf ?wa . ?wa :language bdr:LangBo .} || (not exists {?mw :instanceOf ?wa} && exists {?mw :script ?sc FILTER(?sc = bdr:ScriptTibt || ?sc = bdr:ScriptDbuCan || ?sc = bdr:ScriptDbuMed)}))
      
      ?inDLDw tmp:thumbnailIIIFService ?th .

      bind( true as ?iAccessOpen)
      bind (bda:StatusReleased as ?status)
      ?mw ?ip ?io .
      FILTER (?ip != bdo:note && ?ip != rdf:type && ?ip != bdo:instanceHasReproduction && ?ip != tmp:thumbnailIIIFService && ?ip != bdo:instanceEvent && ?ip != bdo:hasTitle && ?ip != bdo:hasPart
        && ?ip != bdo:colophon && ?ip != bdo:incipit)

    } union {
        ?wadm adm:access bda:AccessOpen ;
              adm:status bda:StatusReleased ;
              adm:adminAbout ?inDLDw .
        FILTER (not exists {?wadm adm:restrictedInChina true })
        FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?th})
        FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
        ?inDLDw :instanceReproductionOf ?mw .
        FILTER (not exists {?mw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) } || exists {?mw :printMethod ?pm FILTER(?pm = bdr:PrintMethod_Manuscript || ?pm = bdr:PrintMethod_Relief_WoodBlock)})
        FILTER (not exists {?inDLDw :instanceHasVolume ?v . ?v :hasIIIFManifest ?iiif })
        ?mw :instanceOf ?wa .
        ?wa :language bdr:LangBo .
      ?wa bdo:creator ?creator .
      ?creator bdo:role ?role .
      ?creator bdo:agent ?agent .
      ?agent skos:prefLabel ?agentL .
      bind( if(?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agent, 1/0) as ?author)

    } union {
      ?wadm adm:access bda:AccessOpen ;
            adm:status bda:StatusReleased ;
            adm:adminAbout ?inDLDw .
      FILTER (not exists {?wadm adm:restrictedInChina true })
      FILTER (exists {?inDLDw tmp:thumbnailIIIFService ?th})
      FILTER (not exists {?inDLDw :inCollection ?c FILTER(?c = bdr:PR1KDPP00 || ?c = bdr:PR1FPL01 )})
      ?inDLDw :instanceReproductionOf ?mw .
      FILTER (not exists {?mw :copyrightStatus ?cs FILTER(?cs = bdr:CopyrightInCopyright || ?cs = bdr:CopyrightClaimed) } || exists {?mw :printMethod ?pm FILTER(?pm = bdr:PrintMethod_Manuscript || ?pm = bdr:PrintMethod_Relief_WoodBlock)})
      FILTER (not exists {?inDLDw :instanceHasVolume ?v . ?v :hasIIIFManifest ?iiif })
      ?mw :instanceOf ?wa .
      ?wa :language bdr:LangBo .
      ?wa ?wp ?wo .
      FILTER(?wp IN(bdo:workIsAbout , bdo:workGenre))
      ?wo skos:prefLabel ?wol .
    }
}
