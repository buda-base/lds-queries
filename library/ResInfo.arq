#QueryScope=General
#QueryReturnType=Graph
#QueryResults=A graph with most relevant triples for a given resource
#QueryParams=R_RES
#QueryUrl=/ResInfo?R_RES=bdr:P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

construct {
 ?R_RES ?rp ?ro .
 ?ro2 ?ro2p ?ro2o .
 ?ro3 ?ro3p ?ro3o .
 ?dt ?dtp ?dto
} where {
  {
    ?R_RES ?rp ?ro .
  } union {
    ?R_RES ?rp2 ?ro2 .
    ?ro2 rdf:type/rdfs:subClassOf+ :Facet .
    FILTER(?ro2 != :eTextHasChunk)
    ?ro2 ?ro2p ?ro2o .
  } union {
    ?R_RES ?rp3 ?ro3 .
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
    OPTIONAL {
      ?ro3 bdo:dateIndication ?dt .    
      ?dt ?dtp ?dto
    }
  } union {
    ?R_RES bf:identifiedBy ?ro4 .
    ?ro4 ?ro4p ?ro4o .
  }
}
