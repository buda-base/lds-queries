#QueryScope=General
#QueryReturnType=Graph
#QueryResults=A graph for the given resource, except if the resource is a non-open access etext
#QueryParams=R_RES
#QueryUrl=/lib/Resgraph?R_RES=bdr:P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

construct { ?s ?p ?o } where {
  ?rAdm adm:graphId ?R_RES ;
        adm:adminAbout ?entity ;
        adm:status ?st .
  FILTER (?st != bda:StatusHidden)
  FILTER (not exists {?rAdm adm:access ?acc FILTER(?acc != bda:AccessOpen)})
  FILTER ((NOT EXISTS {?entity :eTextInInstance ?inst }) || (EXISTS {?entity :eTextInInstance/^adm:adminAbout/adm:access bda:AccessOpen}))
    graph ?R_RES { ?s ?p ?o }
}
