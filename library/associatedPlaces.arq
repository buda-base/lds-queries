#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All places associated with the given entity
#QueryParams=R_RES
#QueryUrl=/lib/associatedPlaces?R_RES=bdr:W19740

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced entity

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
    ?R_RES skos:prefLabel ?resL .
    ?res tmp:status ?st .
    ?res tmp:isMain true .
    ?res ?resp ?reso .

    ?res tmp:placeAffiliation ?trad .
    ?res tmp:provider ?provider .
    ?res tmp:relationType ?rel1 .
    ?res tmp:relationType ?rel2 .
    ?res tmp:relationTypeInv ?relInv .

    ?locatedin skos:prefLabel ?locatedinL .
}
where {
  # And we have 4 types of matches:
  #   * direct relation from/to
  #   * an entity having an event with an eventWhere
  #   * a person in an eventWho, with an eventWhere
  {
    ?R_RES skos:prefLabel ?resL .
  } union {
    {
      ?R_RES ?relInv ?res .
      ?res a bdo:Place .
    } union {
      ?res ?rel1 ?R_RES .
      ?res a bdo:Place .
    } union {
      ?R_RES bdo:placeContains{2,} ?res .
      ?res a bdo:Place .
      BIND(tmp:placeContainsDesc as ?rel1)
    } union {
      ?R_RES ?evtp ?evt .
      ?evt bdo:eventWhere ?res .
      BIND(tmp:commonEvents as ?rel2)
    } union {
      ?evt bdo:eventWho ?R_RES .
      ?evt bdo:eventWhere ?res .
    }
    
    VALUES ?resp {bdo:altLabel skos:prefLabel skos:placeEvent bdo:placeLat bdo:placeLong bdo:placeType bdo:placeLocatedIn owl:sameAs tmp:entityScore bdo:associatedTradition }
    ?res ?resp ?reso .
    #FILTER (?resp IN(bdo:altLabel , skos:prefLabel , skos:placeEvent , bdo:placeLat , bdo:placeLong , bdo:placeType , bdo:placeLocatedIn , owl:sameAs , tmp:entityScore))
    ?resAdm adm:adminAbout ?res .
    ?resAdm adm:status ?st .
    ?resAdm adm:metadataLegal/adm:provider ?provider .

  } union {
    
    {
      ?R_RES ?relInv ?res .
      ?res a bdo:Place .
    } union {
      ?res ?rel1 ?R_RES .
      ?res a bdo:Place .
    } union {
      ?R_RES ?evtp ?evt .
      ?evt bdo:eventWhere ?res .
      BIND(tmp:commonEvents as ?rel2)
    } union {
      ?evt bdo:eventWho ?R_RES .
      ?evt bdo:eventWhere ?res .
    } union {
      ?R_RES bdo:placeContains{2,} ?res .
      BIND(tmp:placeContainsDesc as ?rel1)
    }
    
    ?res bdo:placeEvent/bdo:placeEventAffiliation ?trad .

  } union {
    
    {
      ?R_RES ?relInv ?res .
      ?res a bdo:Place .
    } union {
      ?res ?rel1 ?R_RES .
      ?res a bdo:Place .
    } union {
      ?R_RES ?evtp ?evt .
      ?evt bdo:eventWhere ?res .
      BIND(tmp:commonEvents as ?rel2)
    } union {
      ?evt bdo:eventWho ?R_RES .
      ?evt bdo:eventWhere ?res .
    } union {
      ?R_RES bdo:placeContains{2,} ?res .
      BIND(tmp:placeContainsDesc as ?rel1)
    }
    
    ?res bdo:placeLocatedIn ?locatedin .
    ?locatedinAdm adm:adminAbout ?locatedin .
    ?locatedinAdm adm:status bda:StatusReleased .
    ?locatedin skos:prefLabel ?locatedinL .
  }
}
