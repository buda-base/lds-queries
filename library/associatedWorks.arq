#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All works whose various literals contain the given L_NAME parameter value, along with their LICENSE + ACCESS + LANGSCRIPT + STATUS facets.
#QueryParams=R_RES
#QueryUrl=/lib/associatedWorks?R_RES=bdr:W19740

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the referenced entity

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?R_RES skos:prefLabel ?resL .
    ?res tmp:status ?st .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch.
    ?res tmp:isMain true .

    ?res tmp:nbInstance ?nbinstances .
    ?res tmp:provider ?provider .
    ?res tmp:originalRecord ?originalRecord .
    ?res tmp:hasOpen ?hasopen .
    ?res tmp:possibleAccess ?haspossible .
    ?res tmp:hasImage ?hasimage .
    ?res tmp:hasEtext ?hasetext .
    ?res tmp:relationType ?rel1 .
    ?res tmp:relationType ?rel2 .
    ?res tmp:relationType ?rel3 .
  
    ?res ?resp ?reso .
    ?res ?respp ?resoo .
 
    ?res tmp:author ?author .
    ?agent skos:prefLabel ?agentL .
    ?res tmp:associatedCentury ?authorC .
    ?res bdo:associatedTradition ?authorT .
  
    ?resoo skos:prefLabel ?ol .
    ?resto skos:prefLabel ?restol .

}
WHERE 
{
  # We have the following ways a work can be related to another thing:
  #  - direct relation to/from
  #  - work has creator R_RES
  {
    ?R_RES skos:prefLabel ?resL .
  } union {
    {
      ?R_RES ?rel1 ?res .
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?res ?rel2 ?R_RES .
      FILTER(?rel2 != bdo:serialMemberOf)
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?aac bdo:agent ?R_RES .
      ?res bdo:creator ?aac .
      ?res a/rdfs:subClassOf* bdo:Work .
      ?aac bdo:role ?rel3 .
    }
    
    #VALUES ?resp {bdo:language skos:altLabel skos:prefLabel rdf:type owl:sameAs tmp:entityScore}
    ?res ?resp ?reso .
    FILTER (?resp IN(bdo:language , skos:altLabel , skos:prefLabel , rdf:type , bdo:workSeriesNumber , bdo:catalogInfo , owl:sameAs , tmp:entityScore))
    ?resAdm adm:adminAbout ?res .
    ?resAdm adm:status ?st .
    ?resAdm adm:metadataLegal/adm:provider ?provider .
    
  } union {
    # topics, genres
    
    {
      ?R_RES ?rel1 ?res .
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?res ?rel2 ?R_RES .
      FILTER(?rel2 != bdo:serialMemberOf)
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?aac bdo:agent ?R_RES .
      ?res bdo:creator ?aac . 
    }
    
    ?res ?restp ?resto .
    FILTER (?resp IN(bdo:workIsAbout , bdo:workGenre))
    ?resto skos:prefLabel ?restol .

  } union {
    # creators
    
    {
      ?R_RES ?rel1 ?res .
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?res ?rel2 ?R_RES .
      FILTER(?rel2 != bdo:serialMemberOf)
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?aac bdo:agent ?R_RES .
      ?res bdo:creator ?aac . 
    }
    
    ?res bdo:creator ?creator .
    ?creator bdo:role ?role .
    ?creator bdo:agent ?agent .
    VALUES ?agentp { skos:prefLabel tmp:associatedCentury bdo:associatedTradition }
    ?agent ?agentp ?agento .
    
    bind( if(?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agent, 1/0) as ?author)
    bind( if(?agentp = skos:prefLabel, ?agento, 1/0) as ?agentL)
    bind( if(?agentp = bdo:associatedTradition, ?agento, 1/0) as ?authorT)
    bind( if(?agentp = tmp:associatedCentury && ?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agento, 1/0) as ?authorC)

  } union {
    
    {
      ?R_RES ?rel1 ?res .
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?res ?rel2 ?R_RES .
      FILTER(?rel2 != bdo:serialMemberOf)
      ?res a/rdfs:subClassOf* bdo:Work .
    } union {
      ?aac bdo:agent ?R_RES .
      ?res bdo:creator ?aac . 
    }
    
    ?res ?respp ?resoo .
    FILTER(?respp IN(bdo:serialMemberOf , bdo:workIsAbout , bdo:workGenre))
    ?resoo skos:prefLabel ?ol .

  } union {
    
    select ?res
           (count (distinct ?instance) as ?nbinstances)
           (max(?instanceAccessPossible) as ?haspossible)
           (max(?instanceAccessOpen) as ?hasopen) 
           (max(?instanceImage) as ?hasimage) 
           (max(?instanceEtext) as ?hasetext) 
    where {
      
      {
        ?R_RES ?rel1 ?res .
        ?res a/rdfs:subClassOf* bdo:Work .
      } union {
        ?res ?rel2 ?R_RES .
        FILTER(?rel2 != bdo:serialMemberOf)
        ?res a/rdfs:subClassOf* bdo:Work .
      } union {
        ?aac bdo:agent ?R_RES .
        ?res bdo:creator ?aac . 
      }

      ?res bdo:workHasInstance ?instance .
      ?instance bdo:inRootInstance? ?rootInstance .
      FILTER ( NOT EXISTS {?rootInstance :instanceReproductionOf ?reprof })
      ?rootInstance bdo:instanceHasReproduction? ?instanceRep .
      ?instanceAdm adm:adminAbout ?instanceRep .
      ?instanceAdm adm:status bda:StatusReleased .
      bind( (exists {?instanceAdm adm:access bda:AccessOpen}) && not exists {?instanceAdm adm:syncAgent  bdr:SAOPT} && (?instanceEtext || ?instanceImage) as ?instanceAccessOpen )
      bind( exists {?instanceRep a bdo:ImageInstance . values ?thp { tmp:thumbnailIIIFService tmp:thumbnailIIIFSelected } ?instanceRep ?thp  ?th } as ?instanceImage)
      bind( exists {?instanceRep a bdo:EtextInstance} as ?instanceEtext)
      bind( (exists {?instanceAdm adm:access ?acc . FILTER(?acc IN(bda:AccessOpen , bda:AccessFairUse , bda:AccessRestrictedSealed))}) && (?instanceEtext || ?instanceImage) as ?instanceAccessPossible )

    } group by ?res
  }
}
