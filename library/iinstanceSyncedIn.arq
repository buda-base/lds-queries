#QueryScope=ImageInstance
#QueryReturnType=Graph
#QueryResults=image instances synced in interval
#QueryParams=D_START,D_END
#QueryUrl=/iinstanceSyncedIn?D_START=2020-08-20T00:00:00&D_END=2020-08-27T00:00:00

#param.D_START.type=datetime
#param.D_START.subtype=xsd:dateTime
#param.D_START.desc=the start of the interval we're searching
#param.D_END.type=datetime
#param.D_END.subtype=xsd:dateTime
#param.D_END.desc=the end of the interval we're searching

construct {
    ?ii tmp:isMain true .
    ?ii tmp:lastSync ?sdate .
    ?ii tmp:status ?status .
    ?ii tmp:provider ?provider .
    ?ii tmp:hasOpen ?iAccessOpen .
    ?ii tmp:hasReproAccess ?access .
    ?ii tmp:hasImage true .
    ?ii tmp:hasEtext false .
    ?ii tmp:yearStart ?yearStart .
    ?ii tmp:yearEnd ?yearEnd .
    ?ii a bdo:ImageInstance .
    ?ii :instanceReproductionOf ?i .
    ?ii ?ip ?io .
    ?ii ?iip ?iio .

    # We cheat a bit and attach the topic to the instance instead
    # of the corresponding work
    ?ii ?wp ?wo .
    ?wo skos:prefLabel ?wol .

    # Same for the author, entity score and language
    ?ii tmp:author ?author .
    ?agent skos:prefLabel ?agentL .
    ?ii tmp:associatedCentury ?authorC .
    ?ii bdo:associatedTradition ?authorT .

    # put some of the work properties into the iinstance
    ?ii ?wiip ?wiio .

  } where {
    {
      ?iiAdm tmp:lastSync ?sdate .
      FILTER(?sdate > ?D_START && ?sdate < ?D_END)
      bind( exists {?iiAdm adm:access bda:AccessOpen} as ?iAccessOpen)
      ?iiAdm adm:status ?status .
      ?iiAdm adm:access ?access .
      ?iiAdm adm:metadataLegal/adm:provider ?provider .
      ?iiAdm adm:adminAbout ?ii .
      ?ii ?iip ?iio .
      FILTER (?iip = tmp:thumbnailIIIFService || ?iip = bdo:scanInfo || ?iip = :qualityGrade)
      ?ii :instanceReproductionOf ?i .
      ?i ?ip ?io .
      FILTER (?ip != bdo:note && ?ip != rdf:type && ?ip != bdo:instanceHasReproduction && ?ip != tmp:thumbnailIIIFService && ?ip != bdo:instanceEvent && ?ip != bdo:hasTitle && ?ip != bdo:hasPart
        && ?ip != bdo:colophon && ?ip != bdo:incipit)
    } union {
      ?iiAdm tmp:lastSync ?sdate .
      FILTER(?sdate > ?D_START && ?sdate < ?D_END)
      ?iiAdm adm:adminAbout ?ii .
      ?ii bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w bdo:creator ?creator .
      ?creator bdo:role ?role .
      ?creator bdo:agent ?agent .
      VALUES ?agentp { skos:prefLabel tmp:associatedCentury bdo:associatedTradition }
      ?agent ?agentp ?agento .
    
      bind( if(?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agent, 1/0) as ?author)
      bind( if(?agentp = skos:prefLabel, ?agento, 1/0) as ?agentL)
      bind( if(?agentp = bdo:associatedTradition, ?agento, 1/0) as ?authorT)
      bind( if(?agentp = tmp:associatedCentury && ?role IN(bdr:R0ER0014 , bdr:R0ER0016 , bdr:R0ER0019 , bdr:R0ER0025), ?agento, 1/0) as ?authorC)
    } union {
      ?iiAdm tmp:lastSync ?sdate .
      FILTER(?sdate > ?D_START && ?sdate < ?D_END)
      ?iiAdm adm:adminAbout ?ii .
      ?ii bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w ?wiip ?wiio .
      FILTER(?wiip IN(bdo:language , bdo:catalogInfo))
    } union {
      ?iiAdm tmp:lastSync ?sdate .
      FILTER(?sdate > ?D_START && ?sdate < ?D_END)
      ?iiAdm adm:adminAbout ?ii .
      ?ii bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w ?wp ?wo .
      FILTER(?wp IN(bdo:workIsAbout , bdo:workGenre))
      ?wo skos:prefLabel ?wol .
    } union {
      ?iiAdm tmp:lastSync ?sdate .
      FILTER(?sdate > ?D_START && ?sdate < ?D_END)
      ?iiAdm adm:adminAbout ?ii .
      ?ii bdo:instanceReproductionOf/:instanceEvent ?evt .
      ?evt ?evtp ?evto .
      BIND (IF(?evtp = bdo:onYear || ?evtp = bdo:notBefore, ?evto, 1/0) as ?yearStart)
      BIND (IF(?evtp = bdo:notAfter, ?evto, 1/0) as ?yearEnd)
    }
}
