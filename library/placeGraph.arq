#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All places whose various literals contain the given L_NAME parameter value.
#QueryParams=L_NAME,LG_NAME
#QueryUrl=/lib/placeGraph?L_NAME="'od zer"&LG_NAME=bo-x-ewts

#param.L_NAME.type=string
#param.L_NAME.langTag=LG_NAME
#param.L_NAME.isLucene=true
#param.L_NAME.example="ye shes"

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

CONSTRUCT
{
    ?res tmp:status ?st .
    ?res tmp:matchScore ?sc .
    ?res tmp:labelMatch ?labelMatch .
    ?res tmp:noteMatch ?noteMatch .
    ?res tmp:isMain true .
    ?resS ?resp ?reso .
    ?res owl:sameAs ?resS .
    ?res tmp:placeAffiliation ?trad .
    ?res tmp:provider ?provider .
    ?res tmp:originalRecord ?originalRecord .

    ?locatedin skos:prefLabel ?locatedinL .

}
WHERE 
{
  {
    {
        (?res ?sc ?labelMatch) text:query ( bdo:skosLabels ?L_NAME "highlight:" ) .
        ?res a bdo:Place .
    } union {
        (?note ?sc ?noteMatch) text:query ( bdo:noteText ?L_NAME "highlight:" ) .
        ?res bdo:note ?note .
        ?res a :Place .
    }

    ?res owl:sameAs* ?resS .
    ?resS ?resp ?reso .
    FILTER (?resp = skos:altLabel || ?resp = skos:prefLabel || ?resp = bdo:associatedTradition || ?resp = skos:placeEvent || ?resp = bdo:placeLat || ?resp = bdo:placeLong || ?resp = bdo:placeType || ?resp = owl:sameAs || ?resp = rdfs:seeAlso || ?resp = tmp:entityScore)
    ?resAdm adm:adminAbout ?res .
    ?resAdm adm:status ?st .
    ?resAdm adm:metadataLegal/adm:provider ?provider .

  } union {
    
    {
        (?res) text:query ( bdo:skosLabels ?L_NAME ) .
        ?res a bdo:Place .
    } union {
        (?note ?sc ?noteMatch) text:query ( bdo:noteText ?L_NAME "highlight:" ) .
        ?res bdo:note ?note .
        ?res a :Place .
    }
    
    ?res bdo:placeLocatedIn+ ?locatedin .
    ?locatedinAdm adm:adminAbout ?locatedin .
    ?locatedinAdm adm:status bda:StatusReleased .
    ?locatedin skos:prefLabel ?locatedinL .

  }
}
