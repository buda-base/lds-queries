#QueryScope=Entity
#QueryReturnType=Graph
#QueryResults=A graph for the schema.org transformation
#QueryParams=R_RES
#QueryUrl=/lib/resForMa?R_RES=bdr:MW22084

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  ?R_RES ?p ?o .
  ?t ?tp ?to .
  ?series ?seriesp ?serieso .
  ?pe ?pep ?peo .
  ?ag ?agp ?ago .
  ?aac ?aacp ?aaco .
  ?id ?idp ?ido .
  ?root ?rootp ?rooto .
  ?wa ?wap ?wao .
} where {
  {
    ?R_RES ?p ?o .
    FILTER(?p != bdo:collectionMember && ?p != bdo:serialHasInstance && ?p != bdo:hasPart)
  }
  union {
    ?R_RES :instanceEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    ?R_RES bf:identifiedBy ?id .
    ?id ?idp ?ido .
  }
  union {
    ?R_RES :inRootInstance ?root .
    ?root ?rootp ?rooto .
  }
  union {
    ?R_RES :serialInstanceOf ?series .
    ?series ?seriesp ?serieso .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa ?wap ?wao .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac ?aacp ?aaco .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac :agent ?ag .
    ?ag ?agp ?ago .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac :agent ?ag .
    ?ag :personEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    ?R_RES :personEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    ?R_RES :placeEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa :workIsAbout ?t .
    ?t ?tp ?to .
  }
  union {
    ?R_RES :instanceOf ?wa .
    ?wa :workGenre ?t .
    ?t ?tp ?to .
  }
}
