#QueryScope=Entity
#QueryReturnType=Graph
#QueryResults=A graph for the schema.org transformation
#QueryParams=R_RES
#QueryUrl=/lib/resForMa?R_RES=bdr:MW22084

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct {
  bdr:MW12827 ?p ?o .
  ?t ?tp ?to .
  ?series ?seriesp ?serieso .
  ?pe ?pep ?peo .
  ?ag ?agp ?ago .
  ?aac ?aacp ?aaco .
  ?id ?idp ?ido .
  ?root ?rootp ?rooto .
  ?wa ?wap ?wao .
} where {
  {
    bdr:MW12827 ?p ?o .
    FILTER(?p != bdo:collectionMember && ?p != bdo:serialHasInstance && ?p != bdo:hasPart)
  }
  union {
    bdr:MW12827 :instanceEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    bdr:MW12827 bf:identifiedBy ?id .
    ?id ?idp ?ido .
  }
  union {
    bdr:MW12827 :inRootInstance ?root .
    ?root ?rootp ?rooto .
  }
  union {
    bdr:MW12827 :serialInstanceOf ?series .
    ?series ?seriesp ?serieso .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa ?wap ?wao .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac ?aacp ?aaco .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac :agent ?ag .
    ?ag ?agp ?ago .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa :creator ?aac .
    ?aac :agent ?ag .
    ?ag :personEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    bdr:MW12827 :personEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    bdr:MW12827 :placeEvent ?pe .
    ?pe ?pep ?peo .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa :workIsAbout ?t .
    ?t ?tp ?to .
  }
  union {
    bdr:MW12827 :instanceOf ?wa .
    ?wa :workGenre ?t .
    ?t ?tp ?to .
  }
}
