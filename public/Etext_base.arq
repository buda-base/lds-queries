#QueryScope=Etexts
#QueryReturnType=Graph
#QueryResults=Basic Info about an etext
#QueryParams=R_RES
#QueryUrl=/Etext_base?R_RES=bdr:UT4CZ5369_I1KG9127_0000

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Etext
#param.R_RES.desc=the etext on which info is needed

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { 
   ?R_RES ?p ?o .
   ?ie ?iep ?ieo .
   ?ev ?evp ?evo .
   ?mw skos:prefLabel ?mwpl .
   ?mwroot skos:prefLabel ?mwrootpl .
   ?R_RES tmp:etextIsPaginated ?eip .
   ?R_RES tmp:hasPages ?eihp .
   ?R_RES tmp:hasEtextVolume ?vl .
   ?R_RES :eTextInInstance ?ie .
   ?ie adm:access ?acc .
   ?ie adm:status ?st .
   ?ei tmp:etextIsPaginated ?eip .
   ?ei tmp:hasPages ?eihp .
  }
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
  } union {
    ?R_RES :eTextForInstance ?mw .
    ?mw skos:prefLabel ?mwpl .
  } union {
    ?R_RES :eTextInInstance ?ie .
    ?ie :instanceReproductionOf ?mwroot .
    FILTER(not exists {?mwroot a :ImageInstance})
    ?mwroot skos:prefLabel ?mwrootpl .
  } union {
    # get image volume ID
    ?R_RES :eTextInVolume ?ev .
    ?ev ?evp ?evo .
  } union {
    BIND(
      exists {?R_RES :eTextInInstance ?ie . ?ie :instanceReproductionOf ?w . ?w a :ImageInstance } || 
      exists {?R_RES :volumeOf ?w . ?w :instanceHasReproduction ?ie . } as ?eip)
  } union {
    # add
    ?vl :eTextVolumeForImageGroup ?R_RES . 
  } union {
    # case of UT, VE or IE
    {
      ?R_RES :eTextInInstance ?ie .
    } union {
      ?R_RES :volumeOf ?ie .
      ?ie a :EtextInstance .
    } union {
      ?R_RES a :EtextInstance .
      BIND(?R_RES as ?ie)
    }
    ?ie ?iep ?ieo .
    ?ieadm adm:adminAbout ?ie ;
           adm:access ?acc ;
           adm:status ?st .
    BIND(exists {?ie :instanceHasVolume ?v . ?v :volumePagesTotal ?vpt . FILTER(?vpt > 0)} || exists {?ie :instanceReproductionOf ?w . ?w a :ImageInstance } as ?eihp)
  } union {
    # in the image viewer, this query is called with the image group as R_RES, ex: bdr:I1KG15042
    ?R_RES :volumeOf ?w .
    ?w a :ImageInstance ;
       :instanceHasReproduction ?ie .
    ?ie a :EtextInstance .
    ?ie ?iep ?ieo .
    ?ieadm adm:adminAbout ?ie ;
           adm:access ?acc ;
           adm:status ?st .
    # BIND(exists {?ie :instanceHasVolume ?v . ?v :volumePagesTotal ?vpt . FILTER(?vpt > 0)} as ?eihp)
    # we currently assume it's always true, doing this request would require reimporting the openpecha stuff
    BIND(true as ?eihp)
  }
} 
