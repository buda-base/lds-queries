#QueryScope=ImageInstance
#QueryReturnType=Table
#QueryResults=All scans that must be in the buddhist-digital-resource-center-restricted IA collection
#QueryUrl=/IA_mustbeinbdrcrestricted
#QueryLimit=30000

select distinct ?w where {
  {
    ?wadm adm:access ?acc ;
          adm:status ?st .
    FILTER(?acc != bda:AccessOpen || ?st != bda:StatusReleased)
    ?wadm adm:adminAbout ?w .
    ?w a :ImageInstance .
  } union {
    VALUES ?cs { bdr:CopyrightClaimed bdr:CopyrightInCopyright }
    ?mw :copyrightStatus ?cs .
    ?mw :instanceHasReproduction ?w .
    ?w a :ImageInstance .
    ?wadm adm:adminAbout ?w ;
          adm:access bda:AccessOpen .
  } union {
    ?w :inCollection bdr:PR1NLM00 .
  }
}
