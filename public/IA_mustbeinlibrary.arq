#QueryScope=ImageInstance
#QueryReturnType=Table
#QueryResults=All scans that must be in the inlibrary IA collection
#QueryUrl=/IA_mustbeinlibrary
#QueryLimit=30000

select distinct ?w where {
  {
    ?wadm adm:access bda:AccessFairUse ;
          adm:adminAbout ?w .
    FILTER (NOT EXISTS {?w :digitalLendingPossible false})
    ?w a :ImageInstance .
    FILTER (NOT EXISTS {?w :inCollection bdr:PR1NLM00})
  } union {
    VALUES ?cs { bdr:CopyrightClaimed bdr:CopyrightInCopyright }
    ?mw :copyrightStatus ?cs .
    ?mw :instanceHasReproduction ?w .
    ?w a :ImageInstance .
    ?wadm adm:adminAbout ?w ;
          adm:access bda:AccessOpen .
    FILTER (NOT EXISTS {?w :digitalLendingPossible false})
    FILTER (NOT EXISTS {?w :inCollection bdr:PR1NLM00})
  }
}
