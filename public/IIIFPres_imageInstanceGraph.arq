#QueryScope=ImageInstance
#QueryReturnType=Graph
#QueryResults=Info about an item for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_itemGraph?R_RES=bdr:W22084

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:ImageInstance
#param.R_RES.desc=the item on which info is needed

construct {
   ?s ?p ?o .
   ?cl ?clp ?clo .
   ?ri :instanceHasVolume ?s .
   ?l ?lp ?lo .
} where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
  } union {
    ?R_RES :inRootInstance?/^adm:adminAbout ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :inRootInstance? ?ri .
    ?ri :instanceHasVolume ?s .
    ?s ?p ?o .
    FILTER (sameTerm(?p, :volumeNumber) || sameTerm(?p, skos:prefLabel) || sameTerm(?p, :hasIIIFManifest))
  } union {
    ?R_RES :contentLocation ?l .
    ?l ?lp ?lo .
  }
}
