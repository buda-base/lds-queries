#QueryScope=Instance
#QueryReturnType=Graph
#QueryResults=Info about an instance for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_instanceGraph_noItem?R_RES=bdr:MW22084

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Instance
#param.R_RES.desc=the work on which info is needed

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { 
  ?s ?p ?o .
  ?o a ?ot .
  ?o skos:prefLabel ?ol .
  ?lt a ?ltt .
  ?cl ?clp ?clo .
  ?R_RES tmp:rootAccess ?access .
  ?R_RES tmp:rootRestrictedInChina ?ric .
  ?R_RES tmp:rootStatus ?status .
  ?R_RES tmp:inImageInstance ?iinstance .
  ?R_RES tmp:firstImageGroup ?vol .
}
where {
  { 
    ?R_RES ?p ?o .
    BIND (?R_RES as ?s)
  } union { 
    ?R_RES :virtualLinkTo ?s .
    ?s ?p ?o .
  } union { 
    ?R_RES :contentLocation/:contentLocationInstance/:instanceReproductionOf ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :virtualLinkTo?/:instanceOf/:creator/skos:prefLabel ?o .
    BIND (?R_RES as ?s)
    BIND (tmp:workCreatorLit as ?p)
  } union {
    ?R_RES :virtualLinkTo?/:contentLocation ?s .
    ?s ?p ?o .
  } union {
    {
      ?R_RES :virtualLinkTo?/:inRootInstance?/:instanceHasReproduction ?iinstance .
    } union {
      ?R_RES :contentLocation/:contentLocationInstance/:instanceHasReproduction? ?iinstance .
    }
    ?iinstance a :ImageInstance .
    ?iinstance :inRootInstance? ?iir .
    ?admItem adm:adminAbout ?iir .
    ?admItem adm:access ?access .
    ?admItem adm:status ?status .
    ?admItem adm:restrictedInChina ?ric .
  } union {
    ?R_RES :virtualLinkTo?/:inRootInstance? ?rw .
    ?rw :instanceHasReproduction ?iinstance .
    ?iinstance a :ImageInstance .
    ?R_RES :virtualLinkTo?/:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    ?iinstance :instanceHasVolume ?vol .
    ?vol :volumeNumber ?bvol .
  } union {
    ?R_RES :virtualLinkTo?/:hasPart ?s .
    ?s ?p ?o .
  } union {
    ?R_RES :virtualLinkTo?/:hasPart ?s .
    ?s :contentLocation ?cl .
    ?cl ?clp ?clo .
    FILTER (?clp IN(:contentLocationVolume , :contentLocationEndVolume))
  } union {
    ?R_RES :hasPart ?s .
    ?s :virtualLinkTo ?o .
    ?o a ?ot .
    ?o skos:prefLabel ?ol .
  }
}
