#QueryScope=Volume
#QueryReturnType=Graph
#QueryResults=Outline of a volume for IIIF presentation API
#QueryParams=R_RES
#QueryUrl=/IIIFPres_volumeOutline?R_RES=bdr:I0886

#param.R_RES.type=URI
#param.R_RES.subtype=:Volume
#param.R_RES.desc=the URI of the volume being investigated

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

construct { 
  ?R_RES ?p ?o .
  ?iinstance :instanceOf ?instance .
  ?R_RES tmp:rootAccess ?access .
  ?R_RES tmp:rootStatus ?status .
  ?R_RES tmp:rootRestrictedInChina ?ric .
  ?instance adm:access ?wa .
  ?instance :hasPart ?wp .
  #?part ?pp ?op .
  ?ascPart ?pap ?oap .
  ?loc ?pl ?ol .
}
where {
  {
    {
      ?R_RES :volumeOf ?iinstance ;
             :volumeNumber ?volnum .
      ?loc :contentLocationInstance ?iinstance .
      ?loc :contentLocationVolume ?bvol .
      # see https://lists.apache.org/thread.html/ec4c7fb703748e7d0d70d8e3baa1c2fbb04842b7494c9097df7dfd0f@%3Cusers.jena.apache.org%3E
      FILTER ((?bvol <= ?volnum && NOT EXISTS {?loc :contentLocationEndVolume ?evol}) || (?bvol <= ?volnum  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol <= ?volnum)}))
    }
    ?instance :hasPart ?wp .
    ?loc ?pl ?ol .
    ?part :contentLocation ?loc .
    #?part ?pp ?op .
    ?part :partOf* ?ascPart .
    optional {?ascPart :partOf ?wpo}
    filter (bound(?wpo))
    ?ascPart ?pap ?oap .
  } union {
    ?R_RES :volumeOf ?iinstance .
    ?R_RES ?p ?o .
    ?iinstance :instanceReproductionOf ?instance .
    ?admVol adm:adminAbout ?R_RES .
    ?admItem adm:adminAbout ?iinstance .
    ?admItem adm:access ?access .
    ?admItem adm:status ?status .
    ?admItem adm:restrictedInChina ?ric .
  }
}
