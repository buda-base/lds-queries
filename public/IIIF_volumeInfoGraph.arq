#QueryScope=Volume
#QueryReturnType=Graph
#QueryResults=Basic info about a volume for the IIIF Presentation API
#QueryParams=R_RES
#QueryUrl=/IIIF_volumeInfoGraph?R_RES=bdr:I0886

#param.R_RES.type=URI
#param.R_RES.subtype=:Volume
#param.R_RES.desc=the URI of the volume being investigated

construct {
  ?R_RES ?resp ?reso .
  ?iinstanceId :instanceHasVolume ?R_RES ;
         :instanceReproductionOf ?instanceId ;
         :inCollection  ?col ;
         tmp:thumbnailIIIFService  ?th ;
         :copyrightStatus ?cs .
  ?admItem adm:adminAbout ?iinstanceId .
  ?admItem adm:access ?access .
  ?admItem adm:status ?status .
  ?admItem adm:restrictedInChina ?ric .
} where {
  {
    ?R_RES a :ImageGroup .
    ?R_RES ?resp ?reso .
    ?iinstanceId :instanceHasVolume ?R_RES .
    ?admItem adm:adminAbout ?iinstanceId .
    ?admItem adm:access ?access .
    ?admItem adm:status ?status .
    ?admItem adm:restrictedInChina ?ric .
  } union {
    ?iinstanceId :instanceHasVolume ?R_RES ;
         :instanceReproductionOf ?instanceId .
  } union {
    # collections
    ?iinstanceId :instanceHasVolume ?R_RES ;
                 :inCollection  ?col .
  } union {
    # thumbnail
    ?iinstanceId :instanceHasVolume ?R_RES ;
                 tmp:thumbnailIIIFService  ?th .
  } union {
    # copyright status
    ?iinstanceId :instanceHasVolume ?R_RES ;
         :instanceReproductionOf ?instanceId .
    ?instanceId :copyrightStatus ?cs .
  }
}
