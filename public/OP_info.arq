#QueryScope=General
#QueryReturnType=Graph
#QueryResults=info needed for OpenPecha
#QueryParams=R_RES
#QueryUrl=/OP_info?R_RES=bdr:W12827

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Entity
#param.R_RES.desc=the image instance on which info is needed


# for OpenPecha, gathers info about a W record:
#   - work, author, name of the author, etc.

construct {
    ?R_RES ?resp ?reso .
    ?i ?ip ?io .
    ?w ?wp ?wo .
    ?aac ?aacp ?aaco .
    ?a skos:prefLabel ?al .
    ?adm ?admp ?admo .
    ?ig ?igp ?igo .
    ?l ?lp ?lo .
    ?o :outlineOf ?R_RES .
} where {
   {
      ?R_RES ?resp ?reso .
   } union {
      ?R_RES bdo:instanceReproductionOf ?i .
      ?i ?ip ?io .
   } union{
      ?R_RES bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w ?wp ?wo .
   } union {
      ?R_RES bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w bdo:creator ?aac .
      ?aac ?aacp ?aaco .
      ?aac bdo:agent ?a .
      ?a skos:prefLabel ?al .
   } union {
      ?R_RES bdo:instanceReproductionOf/bdo:instanceOf ?w .
      ?w bdo:language ?l .
      ?l ?lp ?lo .
   } union {
      ?adm adm:adminAbout ?R_RES .
      ?adm ?admp ?admo .
   } union {
      ?R_RES :instanceHasVolume ?ig .
      ?ig ?igp ?igo .
      ?ig :volumePagesTotal ?pt .
      FILTER(?pt > 0)
   } union {
      ?R_RES bdo:instanceReproductionOf ?i .
      ?o :outlineOf ?i .
      ?oadm adm:adminAbout ?o ;
            adm:status bda:StatusReleased .
   }
}
