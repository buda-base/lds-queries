#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All subnodes of this outline node
#QueryParams=R_RES
#QueryUrl=/Outline_root?R_RES=bdr:MW12827

#param.R_RES.type=resource
#param.R_RES.subtype=Instance Resource ID
#param.R_RES.desc=Instance resource ID (can be a part of an instance)
#param.R_RES.example=bdr:MW12827

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>



CONSTRUCT
{
  ?repr :instanceHasVolume ?vol .
  ?vol ?volp ?volo .
  ?part ?partp ?parto .
  ?part   :hasTitle ?partT ;
          :instanceOf ?partW ;
          :contentLocation ?cl ;
          :note ?n ;
          :contentLocationStatement ?cls ;
          bf:identifiedBy ?id ;
          tmp:hasNonVolumeParts ?hasNonVolumeParts ;
          :instanceHasReproduction ?repr .
  ?partT ?partTp ?partTo .
  ?cl ?clp ?clo .
  ?n ?np ?no .
  ?id ?idp ?ido .
  ?part tmp:author ?partWc .
  ?partW skos:prefLabel ?partWL .
  ?partWc skos:prefLabel ?partWcL .
  ?ancestor ?ancestorp ?ancestoro ;
            tmp:hasNonVolumeParts ?ancestorHasNonVolumeParts .
}
WHERE 
{
  {
    # get ancestors label, test with bdr:MW12827_891768 to see some results
    ?R_RES :partOf+ ?ancestor .
    BIND(EXISTS{?ancestor bdo:hasPart ?p . ?p bdo:partType ?pt FILTER(?pt != bdr:PartTypeVolume && ?pt != bdr:PartTypeSection)} as ?ancestorHasNonVolumeParts)
    ?ancestor ?ancestorp ?ancestoro .
    FILTER (?ancestorp IN (skos:prefLabel , :partTreeIndex , :partType , :partIndex , :hasPart))
  #} union {
  	# get volumes
  #	?R_RES :instanceHasReproduction ?repr .
  #	?repr :instanceHasVolume ?vol .
  # ?vol ?volp ?volo .
  # FILTER (?volp IN(bdo:volumeNumber , skos:prefLabel ))
  } union {
  	# get parts: essential triples
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
    ?part ?partp ?parto .
    FILTER (?partp IN (skos:prefLabel , :partTreeIndex , :partType , :partIndex , :hasPart, rdfs:seeAlso, :colophon, :incipit, :explicit,:authorshipStatement ) )
     BIND(EXISTS{?part bdo:hasPart ?p . ?p bdo:partType ?pt FILTER(?pt != bdr:PartTypeVolume && ?pt != bdr:PartTypeSection)} as ?hasNonVolumeParts)
  } union {
    # get parts: essential triples
    ?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
    ?part :instanceHasReproduction ?repr .
    ?repr a :ImageInstance .
  } union {
  	# get parts: title
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
  	?part  :hasTitle ?partT .
  	?partT ?partTp ?partTo .
  } union {
    # get parts: identifiers
    ?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
    ?part  bf:identifiedBy ?id .
    ?id ?idp ?ido .
  } union {
  	# get parts: work triples
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
  	?part  :instanceOf ?partW .
  	?partW skos:prefLabel ?partWL .
  } union {
  	# get parts: work creator triples
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
  	?part :instanceOf ?partW .
  	?partW :creator/:agent ?partWc .
  	?partWc skos:prefLabel ?partWcL .
  } union {
  	# get parts: location triples
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
  	?part :contentLocation ?cl .
  	?cl ?clp ?clo .
  } union {
    # get parts: location triples
    ?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
    ?part :note ?n .
    ?n ?np ?no .
  } union {
  	# get parts: locationstatement triples
  	?R_RES :hasPart? ?part .
    FILTER(NOT EXISTS {
      ?outline :outlineOf ?part .
      ?outlineadm adm:adminAbout ?outline ;
                  adm:status ?status .
      FILTER(?status != bda:StatusReleased)
    })
  	?part :contentLocationStatement ?cls .
  }
}
