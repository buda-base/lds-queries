#QueryScope=General
#QueryReturnType=Graph
#QueryResults=All subnodes of this outline node in a certain volume
#QueryParams=R_RES,I_VNUM
#QueryUrl=/Outline_root?R_RES=bdr:MW23703_S0002&I_VNUM=3

#param.R_RES.type=resource
#param.R_RES.subtype=Instance Resource ID
#param.R_RES.desc=Instance resource ID (can be a part of an instance)
#param.R_RES.example=bdr:MW12827

PREFIX tmp: <http://purl.bdrc.io/ontology/tmp/>

# also try with R_RES=bdr:MW22084_S0010 & I_VNUM = 46

CONSTRUCT
{
  ?repr :instanceHasVolume ?vol .
  ?vol ?volp ?volo .
  ?part ?partp ?parto .
  ?part   :hasTitle ?partT ;
          :instanceOf ?partW ;
          :contentLocation ?cl ;
          :contentLocationStatement ?cls ;
          bf:identifiedBy ?id ;
          :instanceHasReproduction ?repr .
  ?partT ?partTp ?partTo .
  ?cl ?clp ?clo .
  ?id ?idp ?ido .
  ?part tmp:author ?partWc .
  ?partW skos:prefLabel ?partWL .
  ?partWc skos:prefLabel ?partWcL .
  ?ancestor ?ancestorp ?ancestoro .
  ?R_RES tmp:firstImageGroup ?firstig .
}
WHERE 
{
  {
    # get ancestors label, test with bdr:MW12827_891768 to see some results
    ?R_RES :partOf* ?ancestor .
    ?ancestor ?ancestorp ?ancestoro .
    FILTER (?ancestorp IN (skos:prefLabel , :partTreeIndex , :partType , :partIndex , :hasPart))
  } union {
    ?R_RES bdo:inRootInstance?/bdo:instanceHasReproduction ?ii .
    ?ii a bdo:ImageInstance .
    ?ii bdo:instanceHasVolume ?firstig .
    ?firstig bdo:volumeNumber ?I_VNUM .
  } union {
  	# get parts: essential triples
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
    ?part ?partp ?parto .
    FILTER (?partp IN (skos:prefLabel , :partTreeIndex , :partType , :partIndex , :hasPart, rdfs:seeAlso, :colophon, :incipit, :explicit,:authorshipStatement ) )
  } union {
    # get parts: essential triples
    ?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
    ?part :instanceHasReproduction ?repr .
    ?repr a :ImageInstance .
  } union {
  	# get parts: title
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
  	?part  :hasTitle ?partT .
  	?partT ?partTp ?partTo .
  } union {
    # get parts: identifiers
    ?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
    ?part  bf:identifiedBy ?id .
    ?id ?idp ?ido .
  } union {
  	# get parts: work triples
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
  	?part  :instanceOf ?partW .
  	?partW skos:prefLabel ?partWL .
  } union {
  	# get parts: work creator triples
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
  	?part :instanceOf ?partW .
  	?partW :creator/:agent ?partWc .
  	?partWc skos:prefLabel ?partWcL .
  } union {
  	# get parts: location triples
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
  	?part :contentLocation ?cl .
  	?cl ?clp ?clo .
  } union {
  	# get parts: locationstatement triples
  	?R_RES :hasPart? ?part .
    ?part bdo:contentLocation ?loc .
    ?loc :contentLocationVolume ?bvol .
    FILTER (?part = ?R_RES || (?bvol = ?I_VNUM || (?bvol <= ?I_VNUM  && EXISTS {?loc :contentLocationEndVolume ?evol FILTER (?evol >= ?I_VNUM)})))
  	?part :contentLocationStatement ?cls .
  }
}
