#QueryScope=General
#QueryReturnType=Graph
#QueryResults=A graph for the given resource.
#QueryParams=R_RES
#QueryUrl=/ResInfo-SameAs?R_RES=bdr:P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

construct {
 ?r ?rpkeep ?rokeep .
 ?ro2 ?ro2p ?ro2o .
 ?ro3 ?ro3p ?ro3o .
 ?ro3o ?ro4p ?ro4o .
 ?ro5o ?ro6p ?ro6o .

 ?R_RES tmp:hasPartB ?hasPartB ;
        tmp:hasNonVolumeParts ?hasNonVolumeParts .

 ?q ?qp ?qo .
 ?r tmp:hasAdminData ?q .

 ?r tmp:siblingInstances ?siblingNonRepro .
 ?r tmp:siblingReproInstances ?siblingRepro .

 ?rokeep tmp:withSameAs ?roSame .
 
 # misc props needed to fix display:

 ?ro skos:prefLabel ?roLabel .

 ?ro bdo:inRootInstance ?roRoot .
 ?roRoot skos:prefLabel ?roRootLabel .

 ?siblingNonRepro skos:prefLabel ?siblingNonReproLabel .
 ?siblingNonRepro bdo:inRootInstance ?siblingRoot .
 ?siblingRoot skos:prefLabel ?siblingRootLabel .

 ?ro bdo:language ?roLang .

 ?roTCL ?roTCLp ?roTCLo .
 ?roTCLo skos:prefLabel ?roTCLoLabel .

 ?roEv skos:prefLabel ?roEvLab . 

 ?ro6o skos:prefLabel ?ro6oLabel . 

 ?qo skos:prefLabel ?qoLab .

 ?ro adm:canonicalHtml ?roCanonHtml .

 ?roP :partIndex ?roPi .
 ?roV :volumeNumber ?roVi .

 ?id ?idp ?ido .
 ?title ?titlep ?titleo .
 ?cm skos:prefLabel ?cmL .


 ?R_RES bdo:collectionMember ?resInColl .
 ?resInColl skos:prefLabel ?resInCollLabel ;
            tmp:thumbnailIIIFService ?resInCollThumbnail .
 
 ?R_RES bdo:serialHasInstance ?serialI .
 ?serialI skos:prefLabel ?serialILabel ;
          tmp:thumbnailIIIFService ?serialIThumbnail .

 ?resAbout bdo:workIsAbout ?R_RES ;
           skos:prefLabel ?resAboutLabel ;
           tmp:entityScore ?resAboutScore ;
           tmp:provider ?resAboutProvider .

 ?resBy    tmp:createdBy ?R_RES ;
           a ?resByType ;
           skos:prefLabel ?resByLabel ;
           tmp:entityScore ?resByScore ;
           tmp:provider ?resByProvider .

# repro+thumbnail for versions
# + provider
?version  :instanceHasReproduction ?repro ;
          tmp:thumbnailIIIFService ?vThumb ;
          tmp:provider ?qvop .

?par tmp:nbTranslations ?nbtrans .

?item ?itemp ?itemo .
?itemid ?itemidp ?itemido .
?itemcoll skos:prefLabel ?itemcolll .

# first text of first volume of etext
?etext tmp:firstVol ?volstart ;
       tmp:firstVolN ?vnumstart ; 
       tmp:firstText ?rstart ;
       tmp:firstTextN ?rnumstart . 
  
# Instances printed at this place
?resAt    tmp:printedAt ?R_RES ;
          a ?resAtType ;
          skos:prefLabel ?resAtLabel ;
          tmp:entityScore ?resAtScore ;
          tmp:provider ?resAtProvider ;
          tmp:thumbnailIIIFService ?resAtThumb .
  
# access for outline nodes
?r adm:access ?nworkacc .
  
# info related to members (series, corporation, collection)
?mb skos:prefLabel ?mbL ;
    tmp:entityScore ?mbSc .

?adm adm:logEntry ?le .
?le ?lep ?leo .
?user skos:prefLabel ?userL .

?r bdo:personEvent ?foundationEvt .
?foundationEvt ?foundationEvtp ?foundationEvto ;
               :eventWhere ?foundationPlace .
?foundationPlace skos:prefLabel ?foundationPlaceL .

# ContentLocation in a Note
?noteCL ?anyProp ?anyVal . 
?noteCLW skos:prefLabel ?noteCLWL .

?R_RES tmp:addIALink ?addIALink .

} where {
  {
    ?r owl:sameAs* ?R_RES .
    ?r ?rpkeep ?rokeep .
    FILTER(?rpkeep != bdo:collectionMember && ?rpkeep != bdo:serialHasInstance && ?rpkeep != bdo:hasPart)
  } union  {
    ?r owl:sameAs* ?R_RES .
    ?r ?rpkeep ?rokeep .
    FILTER(?rpkeep != bdo:collectionMember && ?rpkeep != bdo:serialHasInstance && ?rpkeep != bdo:hasPart)
    ?rokeep owl:sameAs+ ?roSame .
  } union  {
    # monastery foundation events, see https://github.com/buda-base/public-digital-library/issues/556
    ?r owl:sameAs* ?R_RES .
    ?foundationEvt :eventWho ?r .
    ?foundationPlace :placeEvent ?foundationEvt .
    ?foundationEvt ?foundationEvtp ?foundationEvto .
    FILTER (?foundationEvtp != bdo:eventWho)
    ?foundationPlace skos:prefLabel ?foundationPlaceL .
  } union {
    # add prefLabel for any related resource
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    FILTER(?rp != bdo:collectionMember && ?rp != bdo:serialHasInstance && ?rp != bdo:hasPart)
    ?ro skos:prefLabel ?roLabel .
  } union {
    # display inRootInstance (used to sort instances)
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    FILTER(?rp != bdo:collectionMember && ?rp != bdo:serialHasInstance && ?rp != bdo:hasPart)
    ?ro bdo:inRootInstance ?roRoot .
    ?roRoot skos:prefLabel ?roRootLabel .
  } union {
    # add language (used to sort translations)
    ?r owl:sameAs* ?R_RES .
    ?r :workHasTranslation ?ro .
    ?ro bdo:language ?roLang .
  } union {
    # add prefLabel for Event subclasses objects
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    FILTER(?rp != bdo:collectionMember && ?rp != bdo:serialHasInstance && ?rp != bdo:hasPart)
    ?ro rdf:type/rdfs:subClassOf+ :Event .
    ?ro ?rpEv ?roEv .
    ?roEv skos:prefLabel ?roEvLab .
  } union {
    # add prefLabel for LineageHolder objects
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    FILTER(?rp != bdo:collectionMember && ?rp != bdo:serialHasInstance && ?rp != bdo:hasPart)
    ?ro rdf:type/rdfs:subClassOf* :LineageHolder .
    ?ro ?rpEv ?roEv .
    ?roEv skos:prefLabel ?roEvLab .
  } union  {
    # get canonicalHtml for related resources
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    FILTER(?rp != bdo:collectionMember && ?rp != bdo:serialHasInstance && ?rp != bdo:hasPart)
    ?ro adm:canonicalHtml ?roCanonHtml .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp2 ?ro2 .
    FILTER(?rp2 != bdo:collectionMember && ?rp2 != bdo:serialHasInstance && ?rp2 != bdo:hasPart)
    ?ro2 rdf:type/rdfs:subClassOf+ :Facet .
    ?ro2 ?ro2p ?ro2o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    FILTER(?rp3 != bdo:collectionMember && ?rp3 != bdo:serialHasInstance && ?rp3 != bdo:hasPart)
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    FILTER(?rp3 != bdo:collectionMember && ?rp3 != bdo:serialHasInstance && ?rp3 != bdo:hasPart)
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
    ?ro3o rdf:type/rdfs:subClassOf+ :Facet .
    ?ro3o ?ro4p ?ro4o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    FILTER(?rp3 != bdo:collectionMember && ?rp3 != bdo:serialHasInstance && ?rp3 != bdo:hasPart)
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
    ?ro3o a bdo:DateIndication .
    ?ro3o ?ro4p ?ro4o .
  } union {
    # return sub-events related to lineages
    ?r owl:sameAs* ?R_RES .
    ?r rdf:type/rdfs:subClassOf* :Lineage .
    ?r ?rp5 ?ro5 .
    ?ro5 ?ro5p ?ro5o .
    ?ro5o rdf:type/rdfs:subClassOf+ :Event .
    ?ro5o ?ro6p ?ro6o .
  } union {
    # return prefLabels for sub-events related to lineages
    ?r owl:sameAs* ?R_RES .
    ?r rdf:type/rdfs:subClassOf* :Lineage .
    ?r ?rp5 ?ro5 .
    ?ro5 ?ro5p ?ro5o .
    ?ro5o rdf:type/rdfs:subClassOf+ :Event .
    ?ro5o ?ro6p ?ro6o .
    ?ro6o skos:prefLabel ?ro6oLabel . 
  } union {
    ?r owl:sameAs* ?R_RES .
    ?q adm:adminAbout ?r .
    ?q ?qp ?qo .
    # we don't return all the adminAbout, they can be in large quantities
    FILTER(?qp != adm:adminAbout)
  } union {
    # return legalData labels
    ?r owl:sameAs* ?R_RES .
    ?q adm:adminAbout ?r .
    ?q adm:metadataLegal ?qo .
    ?qo skos:prefLabel ?qoLab .
  } union {
    # add access for outline nodes
    ?r owl:sameAs* ?R_RES .
    ?r :contentLocation/:contentLocationInstance ?nwork .
    ?nworkadm adm:adminAbout ?nwork ;
              adm:access ?nworkacc .
  } union {
    # add ContentLocation nodes that were missing
    # + display prefLabel for AgentAsCreator 
    # + add Note subnodes
    ?r owl:sameAs* ?R_RES .
    VALUES ?rpTCL { :contentLocation :creator :note }
    ?r ?rpTCL ?roTCL .
    ?roTCL ?roTCLp ?roTCLo .
  } union {
    # label for previous:
    ?r owl:sameAs* ?R_RES .
    VALUES ?rpTCL { :contentLocation :creator :note }
    ?r ?rpTCL ?roTCL .
    ?roTCL ?roTCLp ?roTCLo .
    ?roTCLo skos:prefLabel ?roTCLoLabel .
  } union {
    # https://github.com/buda-base/public-digital-library/issues/649 (?R_RES = bdr:T111111111)
    # https://github.com/buda-base/public-digital-library/issues/641 (?R_RES = bdr:MW3CN27014) hasNonVolumeParts must be true
    # https://github.com/buda-base/public-digital-library/issues/652 (?R_RES = bdr:MW27302) hasNonVolumeParts must be false
    # https://github.com/buda-base/public-digital-library/issues/730 (?R_RES = bdr:MW22271) hasNonVolumeParts must be false
    BIND(IF(EXISTS{?R_RES a ?any}, EXISTS{?R_RES :hasPart ?p}, 1/0) as ?hasPartB)
    BIND(IF(EXISTS{?R_RES a ?any}, !EXISTS{?R_RES :hasPart ?p . ?p bdo:partType ?pt FILTER(?pt = bdr:PartTypeVolume || ?pt = bdr:PartTypeSection)} && NOT EXISTS{?R_RES :bdo:inRootInstance?/bdo:instanceHasReproduction ?ii .
    ?ii a bdo:ImageInstance ; :numberOfVolumes  1 .}, 1/0) as ?hasNonVolumeParts)
  } union {
    # add volume numbers
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasVolume ?roV .
    ?roV :volumeNumber ?roVi .
  } union {
    # add repro+thumbnail for versions
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    FILTER(exists{?vadm adm:adminAbout ?version ; adm:status bda:StatusReleased })
    ?version :instanceHasReproduction ?repro .
    ?repro a bdo:ImageInstance .
  } union {
    # add repro for versions with inRootInstance
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?version :inRootInstance ?rootV .
    FILTER(exists{?vadm adm:adminAbout ?rootV ; adm:status bda:StatusReleased })
    ?rootV :instanceHasReproduction ?repro .
    ?repro a bdo:ImageInstance .
  } union {
    # add thumbnail for work instances
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?version :inRootInstance? ?rootV .
    ?rootV tmp:thumbnailIIIFService ?vThumb .
  } union {
    # add provider for versions
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    FILTER(exists{?vadm adm:adminAbout ?version ; adm:status bda:StatusReleased })
    ?qv adm:adminAbout ?version .
    ?qv adm:metadataLegal/adm:provider ?qvop .
  } union {
    # add thumbnail for instance reproductions
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction ?version .
    ?version :inRootInstance? ?rootV .
    FILTER(exists{?vadm adm:adminAbout ?rootV ; adm:status bda:StatusReleased })
    ?rootV tmp:thumbnailIIIFService ?vThumb .
  } union {
    # add items with collections
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction*/:instanceHasItem ?item .
    ?item ?itemp ?itemo .
    ?item :inCollection ?itemcoll .
    ?itemcoll skos:prefLabel ?itemcolll .
  } union {
    # add items with collections
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction*/:instanceHasItem ?item .
    ?item bf:identifiedBy ?itemid .
    ?itemid ?itemidp ?itemido .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bf:identifiedBy ?id .
    ?id ?idp ?ido .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bdo:hasTitle ?title .
    ?title ?titlep ?titleo .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bdo:corporationHasMember/bdo:corporationMember ?cm .
    ?cm skos:prefLabel ?cmL .
  } union {
    # score for members of series
    ?r owl:sameAs* ?R_RES ;
       :serialHasMember ?mb .
    ?mb tmp:entityScore ?mbSc .
  } union {
    # prefLabel for members of corporation 
    ?r owl:sameAs* ?R_RES ;
       :corporationHasMember ?mb .
    ?mb :corporationMember/skos:prefLabel ?mbL .
  } union {
    BIND(IF(EXISTS{?R_RES a ?any}, EXISTS {
      ?r owl:sameAs* ?R_RES .
      ?r :inRootInstance?/:instanceHasReproduction? ?w .
      ?w a bdo:ImageInstance .
      ?wadm adm:adminAbout ?w ;
            adm:access bda:AccessFairUse ;
            adm:status bda:StatusReleased .
      FILTER(not exists {?w :digitalLendingPossible false})
    }, 1/0) as ?addIALink)
  } union {
    # 10 works about the entity
    select
       ?resAbout ?resAboutScore ?resAboutLabel ?resAboutProvider
    where {
           ?r owl:sameAs* ?R_RES .
           ?resAbout bdo:workIsAbout ?r ;
                     tmp:entityScore ?resAboutScore ;
                     skos:prefLabel ?resAboutLabel .
           ?resAdm adm:adminAbout ?resAbout ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resAboutProvider .
    } order by desc(?resAboutScore) limit 20
  } union {
    # 10 versions in collection, test with bdr:PR1CTC17, bdr:PR0IDP001, bdr:PR0NGMCP0617
    select
       ?resInColl ?resInCollLabel ?resInCollThumbnail
    where {
      {
         ?resInCollX bdo:inCollection ?R_RES .
         ?resInColl bdo:instanceHasItem? ?resInCollX .
         ?resInColl tmp:thumbnailIIIFService ?resInCollThumbnail .
         ?resInColl bdo:instanceReproductionOf? ?resWithLabel .
         ?resWithLabel skos:prefLabel ?resInCollLabel .
      } union {
        ?resInCollX bdo:inCollection ?R_RES.
        ?resInColl bdo:instanceHasItem? ?resInCollX .
        FILTER(not exists{?resInColl tmp:thumbnailIIIFService ?resInCollThumbnail . })
        ?resInColl bdo:instanceReproductionOf? ?resWithLabel .
        ?resWithLabel skos:prefLabel ?resInCollLabel .
      }
    } limit 20
  } union {
    # 10 instances of serial
    select
       ?serialI ?serialILabel ?serialIThumbnail
    where {
      {
         ?R_RES bdo:serialHasInstance ?serialI .
         ?serialI tmp:thumbnailIIIFService ?serialIThumbnail .
         ?serialI skos:prefLabel ?serialILabel .
      }
    } limit 20
  } union {
    # 10 works created by the entity
    select 
       ?resBy ?resByType ?resByScore ?resByLabel ?resByProvider
    where {
           ?r owl:sameAs* ?R_RES .
           ?resBy bdo:creator/bdo:agent ?r ;
                  a ?resByType ;
                  tmp:entityScore ?resByScore ;
                  skos:prefLabel ?resByLabel .
           ?resAdm adm:adminAbout ?resBy ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resByProvider .
    } order by desc(?resByScore) limit 20
  } union {
    # number of translations by parallel
    select 
       ?par (count(distinct ?trans) as ?nbtrans) 
    where {
           ?r owl:sameAs* ?R_RES .
           ?r bdo:workHasParallelsIn ?par .
           ?par owl:sameAs*/bdo:workHasTranslation ?trans .
    } group by ?par
  } union {    
    # first text of first volume of etext 
    select ?etext ?volstart ?vnumstart ?vtext ?rnumstart ?rstart
    where {
      ?etext owl:sameAs* ?R_RES ;
             :instanceHasVolume ?volstart .
      ?volstart :volumeNumber ?vnumstart ; :volumeHasEtext ?vtext.
      ?vtext :seqNum ?rnumstart ; :eTextResource ?rstart .  
  
      # find first volume 
      { 
        select ?etext ( min(?vnum) as ?vnumstart )  
        where { 
          ?etext owl:sameAs* ?R_RES ;   
                 :instanceHasVolume/:volumeNumber ?vnum .
        } group by ?etext
      }  
      # find first text of first volume
      { 
        select ?volstart ( min(?rnum) as ?rnumstart )
        where {
          ?etext owl:sameAs* ?R_RES ;   
                 :instanceHasVolume ?volstart . 
          ?volstart :volumeNumber ?vnumstart ;
                    :volumeHasEtext/:seqNum ?rnum .
        } group by ?volstart
      }   
    }
  } union {
    # select 10 instances printed at this place
    select ?resAt ?resAtType ?resAtLabel ?resAtProvider ?resAtThumb ?resAtScore
    where {
           ?r owl:sameAs* ?R_RES .
           ?resAt bdo:hasSourcePrintery ?r ;
                  a ?resAtType ;
                  skos:prefLabel ?resAtLabel ;
                  :instanceHasReproduction/tmp:thumbnailIIIFService ?resAtThumb .
           ?resAdm adm:adminAbout ?resAt ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resAtProvider .
    }  order by desc(?resAtScore) limit 20
  } union {
    ?adm adm:adminAbout ?R_RES .
    ?adm adm:logEntry ?le .
    ?le ?lep ?leo .
  } union {
    ?adm adm:adminAbout ?R_RES .
    ?adm adm:logEntry ?le .
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userL .
  } union {
    # add ContentLocation from a Note
    ?r owl:sameAs* ?R_RES .
    ?r :note/:contentLocation ?noteCL .
    ?noteCL ?anyProp ?anyVal ;
        :contentLocationInstance ?noteCLW .
    ?noteCLW :instanceReproductionOf/skos:prefLabel ?noteCLWL .
  }
}
