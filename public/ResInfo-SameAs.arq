#QueryScope=General
#QueryReturnType=Graph
#QueryResults=A graph for the given resource.
#QueryParams=R_RES
#QueryUrl=/ResInfo-SameAs?R_RES=bdr:P360

#param.R_RES.type=resource
#param.R_RES.subtype=a Resource ID
#param.R_RES.desc=the unique resource ID of the chosen resource

construct {
 ?r ?rp ?ro .
 ?ro2 ?ro2p ?ro2o .
 ?ro3 ?ro3p ?ro3o .
 ?ro3o ?ro4p ?ro4o .
 ?ro5o ?ro6p ?ro6o .

 ?q ?qp ?qo .
 ?r tmp:hasAdminData ?q .

 ?r tmp:siblingInstances ?siblingNonRepro .
 ?r tmp:siblingReproInstances ?siblingRepro .

 ?ro tmp:withSameAs ?roSame .
 ?ro tmp:withSameAs ?roSame2 .
 
 # misc props needed to fix display:

 ?ro skos:prefLabel ?roLabel .

 ?ro bdo:inRootInstance ?roRoot .
 ?roRoot skos:prefLabel ?roRootLabel .

 ?siblingNonRepro skos:prefLabel ?siblingNonReproLabel .
 ?siblingNonRepro bdo:inRootInstance ?siblingRoot .
 ?siblingRoot skos:prefLabel ?siblingRootLabel .

 ?ro bdo:language ?roLang .

 ?roTCL ?roTCLp ?roTCLo .
 ?roTCLo skos:prefLabel ?roTCLoLabel .

 ?roEv skos:prefLabel ?roEvLab . 

 ?ro6o skos:prefLabel ?ro6oLabel . 

 ?qo skos:prefLabel ?qoLab .

 ?ro adm:canonicalHtml ?roCanonHtml .

 ?roP :partIndex ?roPi .
 ?roV :volumeNumber ?roVi .

 ?id ?idp ?ido .
 ?title ?titlep ?titleo .
 ?cm skos:prefLabel ?cmL .

 ?resAbout bdo:workIsAbout ?R_RES ;
           skos:prefLabel ?resAboutLabel ;
           tmp:entityScore ?resAboutScore ;
           tmp:provider ?resAboutProvider .

 ?resBy    tmp:createdBy ?R_RES ;
           a ?resByType ;
           skos:prefLabel ?resByLabel ;
           tmp:entityScore ?resByScore ;
           tmp:provider ?resByProvider .

# repro+thumbnail for versions
# + provider
?version  :instanceHasReproduction ?repro ;
          tmp:thumbnailIIIFService ?vThumb ;
          tmp:provider ?qvop .

?par tmp:nbTranslations ?nbtrans .

?item ?itemp ?itemo .
?itemid ?itemidp ?itemido .
?itemcoll skos:prefLabel ?itemcolll .

# first text of first volume of etext
?etext tmp:firstVol ?volstart ;
       tmp:firstVolN ?vnumstart ; 
       tmp:firstText ?rstart ;
       tmp:firstTextN ?rnumstart . 
  
# Instances printed at this place
?resAt    tmp:printedAt ?R_RES ;
          a ?resAtType ;
          skos:prefLabel ?resAtLabel ;
          tmp:entityScore ?resAtScore ;
          tmp:provider ?resAtProvider ;
          tmp:thumbnailIIIFService ?resAtThumb .
  
# access for outline nodes
?r adm:access ?nworkacc .
  
} where {
  {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
  } union  {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro owl:sameAs+ ?roSame .
  } union {
    # add prefLabel for any related resource
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro skos:prefLabel ?roLabel .
  } union {
    # display inRootInstance (used to sort instances)
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro bdo:inRootInstance ?roRoot .
    ?roRoot skos:prefLabel ?roRootLabel .
  } union {
    # add language (used to sort translations)
    ?r owl:sameAs* ?R_RES .
    ?r :workHasTranslation ?ro .
    ?ro bdo:language ?roLang .
  } union {
    # add prefLabel for Event subclasses objects
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro rdf:type/rdfs:subClassOf+ :Event .
    ?ro ?rpEv ?roEv .
    ?roEv skos:prefLabel ?roEvLab .
  } union {
    # add prefLabel for LineageHolder objects
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro rdf:type/rdfs:subClassOf* :LineageHolder .
    ?ro ?rpEv ?roEv .
    ?roEv skos:prefLabel ?roEvLab .
  } union  {
    # get canonicalHtml for related resources
    ?r owl:sameAs* ?R_RES .
    ?r ?rp ?ro .
    ?ro adm:canonicalHtml ?roCanonHtml .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp2 ?ro2 .
    ?ro2 rdf:type/rdfs:subClassOf+ :Facet .
    ?ro2 ?ro2p ?ro2o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
    ?ro3o rdf:type/rdfs:subClassOf+ :Facet .
    ?ro3o ?ro4p ?ro4o .
  } union {
    ?r owl:sameAs* ?R_RES .
    ?r ?rp3 ?ro3 .
    ?ro3 rdf:type/rdfs:subClassOf+ :Event .
    ?ro3 ?ro3p ?ro3o .
    ?ro3o a bdo:DateIndication .
    ?ro3o ?ro4p ?ro4o .
  } union {
    # return sub-events related to lineages
    ?r owl:sameAs* ?R_RES .
    ?r rdf:type/rdfs:subClassOf* :Lineage .
    ?r ?rp5 ?ro5 .
    ?ro5 ?ro5p ?ro5o .
    ?ro5o rdf:type/rdfs:subClassOf+ :Event .
    ?ro5o ?ro6p ?ro6o .
  } union {
    # return prefLabels for sub-events related to lineages
    ?r owl:sameAs* ?R_RES .
    ?r rdf:type/rdfs:subClassOf* :Lineage .
    ?r ?rp5 ?ro5 .
    ?ro5 ?ro5p ?ro5o .
    ?ro5o rdf:type/rdfs:subClassOf+ :Event .
    ?ro5o ?ro6p ?ro6o .
    ?ro6o skos:prefLabel ?ro6oLabel . 
  } union {
    ?r owl:sameAs* ?R_RES .
    ?q adm:adminAbout ?r .
    ?q ?qp ?qo .
    # we don't return all the adminAbout, they can be in large quantities
    FILTER(?qp != adm:adminAbout)
  } union {
    # return legalData labels
    ?r owl:sameAs* ?R_RES .
    ?q adm:adminAbout ?r .
    ?q adm:metadataLegal ?qo .
    ?qo skos:prefLabel ?qoLab .
  } union {
    # add access for outline nodes
    ?r owl:sameAs* ?R_RES .
    ?r :contentLocation/:contentLocationInstance ?nwork .
    ?nworkadm adm:adminAbout ?nwork ;
              adm:access ?nworkacc .
  } union {
    # add ContentLocation nodes that were missing
    # + display prefLabel for AgentAsCreator 
    # + add Note subnodes
    ?r owl:sameAs* ?R_RES .
    ?r ?rpTCL ?roTCL .
    VALUES ?roTCLtype { :ContentLocation :AgentAsCreator :Note }
    ?roTCL a ?roTCLtype .
    ?roTCL ?roTCLp ?roTCLo .
  } union {
    # label for previous:
    ?r owl:sameAs* ?R_RES .
    ?r ?rpTCL ?roTCL .
    VALUES ?roTCLtype { :ContentLocation :AgentAsCreator :Note }
    ?roTCL a ?roTCLtype .
    ?roTCL ?roTCLp ?roTCLo .
    ?roTCLo skos:prefLabel ?roTCLoLabel .
  } union {
    # add parts indexes
    ?r owl:sameAs* ?R_RES .
    ?r :hasPart ?roP .
    ?roP :partIndex ?roPi .
  } union {
    # add volume numbers
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasVolume ?roV .
    ?roV :volumeNumber ?roVi .
  } union {
    # add repro+thumbnail for versions
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?version :instanceHasReproduction ?repro .
    ?repro a bdo:ImageInstance .
  } union {
    # add repro for versions with inRootInstance
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?version :inRootInstance ?rootV .
    ?rootV :instanceHasReproduction ?repro .
    ?repro a bdo:ImageInstance .
  } union {
    # add thumbnail for work instances
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?version :inRootInstance? ?rootV .
    ?rootV tmp:thumbnailIIIFService ?vThumb .
  } union {
    # add provider for versions
    ?r owl:sameAs* ?R_RES .
    ?r :workHasInstance ?version .
    ?qv adm:adminAbout ?version .
    ?qv adm:metadataLegal/adm:provider ?qvop .
  } union {
    # add thumbnail for instance reproductions
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction ?version .
    ?version :inRootInstance? ?rootV .
    ?rootV tmp:thumbnailIIIFService ?vThumb .
  } union {
    # add items with collections
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction*/:instanceHasItem ?item .
    ?item ?itemp ?itemo .
    ?item :inCollection ?itemcoll .
    ?itemcoll skos:prefLabel ?itemcolll .
  } union {
    # add items with collections
    ?r owl:sameAs* ?R_RES .
    ?r :instanceHasReproduction*/:instanceHasItem ?item .
    ?item bf:identifiedBy ?itemid .
    ?itemid ?itemidp ?itemido .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bf:identifiedBy ?id .
    ?id ?idp ?ido .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bdo:hasTitle ?title .
    ?title ?titlep ?titleo .
  } union {
    ?r owl:sameAs* ?R_RES ;
       bdo:corporationHasMember/bdo:corporationMember ?cm .
    ?cm skos:prefLabel ?cmL .
  } union {
    # 10 works about the entity
    select
       ?resAbout ?resAboutScore ?resAboutLabel ?resAboutProvider
    where {
           ?r owl:sameAs* ?R_RES .
           ?resAbout bdo:workIsAbout ?r ;
                     tmp:entityScore ?resAboutScore ;
                     skos:prefLabel ?resAboutLabel .
           ?resAdm adm:adminAbout ?resAbout ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resAboutProvider .
    } order by desc(?resAboutScore) limit 20
  } union {
    # 10 works created by the entity
    select 
       ?resBy ?resByType ?resByScore ?resByLabel ?resByProvider
    where {
           ?r owl:sameAs* ?R_RES .
           ?resBy bdo:creator/bdo:agent ?r ;
                  a ?resByType ;
                  tmp:entityScore ?resByScore ;
                  skos:prefLabel ?resByLabel .
           ?resAdm adm:adminAbout ?resBy ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resByProvider .
    } order by desc(?resByScore) limit 20
  } union {
    # number of translations by parallel
    select 
       ?par (count(distinct ?trans) as ?nbtrans) 
    where {
           ?r owl:sameAs* ?R_RES .
           ?r bdo:workHasParallelsIn ?par .
           ?par owl:sameAs*/bdo:workHasTranslation ?trans .
    } group by ?par
  } union {    
    # first text of first volume of etext 
    select ?etext ?volstart ?vnumstart ?vtext ?rnumstart ?rstart
    where {
      ?etext owl:sameAs* ?R_RES ;
             :instanceHasVolume ?volstart .
      ?volstart :volumeNumber ?vnumstart ; :volumeHasEtext ?vtext.
      ?vtext :seqNum ?rnumstart ; :eTextResource ?rstart .  
  
      # find first volume 
      { 
        select ?etext ( min(?vnum) as ?vnumstart )  
        where { 
          ?etext owl:sameAs* ?R_RES ;   
                 :instanceHasVolume/:volumeNumber ?vnum .
        } group by ?etext
      }  
      # find first text of first volume
      { 
        select ?volstart ( min(?rnum) as ?rnumstart )
        where {
          ?etext owl:sameAs* ?R_RES ;   
                 :instanceHasVolume ?volstart . 
          ?volstart :volumeNumber ?vnumstart ;
                    :volumeHasEtext/:seqNum ?rnum .
        } group by ?volstart
      }   
    }
  } union {
    # select 10 instances printed at this place
    select ?resAt ?resAtType ?resAtLabel ?resAtProvider ?resAtThumb ?resAtScore
    where {
           ?r owl:sameAs* ?R_RES .
           ?resAt bdo:hasSourcePrintery ?r ;
                  a ?resAtType ;
                  skos:prefLabel ?resAtLabel ;
                  :instanceHasReproduction/tmp:thumbnailIIIFService ?resAtThumb .
           ?resAdm adm:adminAbout ?resAt ;
                   adm:status bda:StatusReleased ;
                   adm:metadataLegal/adm:provider ?resAtProvider .
     	   ?wRes   :workHasInstance ?resAt ;
                   tmp:entityScore ?resAtScore .
    }  order by desc(?resAtScore) limit 20
  }
}