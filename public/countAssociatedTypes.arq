#QueryScope=General
#QueryReturnType=Table
#QueryResults=All entities and chunks matching the name
#QueryParams=R_RES
#QueryUrl=/countAssociatedTypes?R_RES=bdr:W19740

#param.R_RES.type=URI
#param.R_RES.subtype=bdo:Entity
#param.R_RES.desc=the resource on which we want to list the associations

# TODO: look at all the queries for more cases, some are missing

select ?type (count (distinct ?e) as ?count) where {
	{
		?R_RES ?ep ?e .
		?e a ?type .
		?type rdfs:subClassOf+ bdo:Entity .
	} union {
		?e ?ep ?R_RES .
		?e a ?type .
		?type rdfs:subClassOf+ bdo:Entity .
	} union {
		?evt ?evtp ?R_RES .
		?e bdo:personEvent ?evt .
		bind(bdo:Person as ?type)
	} union {
		?evt ?evtp ?R_RES .
		?e bdo:placeEvent ?evt .
		bind(bdo:Place as ?type)
	} union {
		?evt ?evtp ?R_RES .
		?e bdo:creator ?evt .
		?e a ?type .
  } union {
    ?lh :lineageWho ?R_RES .
    ?e :lineageHolder ?lh .
    bind(bdo:Lineage as ?type)
  } union {
    ?le :lineageFrom ?R_RES .
    ?lh :lineageReceived ?le .
    ?e :lineageHolder ?lh .
    bind(bdo:Lineage as ?type)
	} union {
    ?it :inCollection ?R_RES .
    ?e :instanceHasItem ?it .
    ?e a ?type .
  }
  FILTER(exists {?eadm adm:adminAbout ?e ; adm:status bda:StatusReleased .} || exists {?e :inRootInstance ?eR . ?eadm adm:adminAbout ?eR ; adm:status bda:StatusReleased .})
} group by ?type 
