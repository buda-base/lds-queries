#QueryScope=EtextInstance
#QueryReturnType=Graph
#QueryResults=Outline of an etext instance for display
#QueryParams=R_RES
#QueryUrl=/etextrefs?R_RES=bdr:IE3JT13307

#param.R_RES.type=URI
#param.R_RES.subtype=:EtextInstance
#param.R_RES.desc=the URI of the etext instance

construct {
	?R_RES :instanceHasVolume ?vol .
	?vol ?volp ?volo .
	?eref ?erefp ?erefo .
	?ut skos:prefLabel ?utl .
  ?utd skos:prefLabel ?utdl .
  ?vol :volumeHasEtext [
     :seqNum ?sn ;
     :etextResource ?utd .
  ]
} where {
  {
  	?R_RES :instanceHasVolume ?vol .
  	?vol :volumeHasEtext ?eref .
    ?eref :eTextResource ?ut .
    ?ut skos:prefLabel ?utl .
  	?eref ?erefp ?erefo .
    ?vol ?volp ?volo .
  } union {
    # here we construct a virtual EtextRef for the transition
  	?R_RES :instanceHasVolume ?vol .
	  ?vol :volumeHasEtext ?utd .
    ?utd skos:prefLabel ?utdl ;
         :eTextVolumeIndex ?sn . 
	  ?vol ?volp ?volo .
    FILTER(?volp != :volumeHasEtext)
  }
}
