#QueryScope=EtextInstance
#QueryReturnType=Graph
#QueryResults=Outline of an etext instance for display
#QueryParams=R_RES
#QueryUrl=/etextrefs?R_RES=bdr:IE3JT13307

#param.R_RES.type=URI
#param.R_RES.subtype=:EtextInstance
#param.R_RES.desc=the URI of the etext instance

construct {
  ?R_RES ?iep ?ieo .
  ?vol ?volp ?volo .
  ?vol skos:prefLabel ?volpl .
  ?ut ?utp ?uto .
  ?ut skos:prefLabel ?utpl .
  ?vol :volumeHasEtext ?ut .
} where {
  {
    ?R_RES ?iep ?ieo .
  } union {
    ?R_RES :instanceHasVolume ?vol .
    ?vol ?volp ?volo .
  } union {
    ?R_RES :instanceHasVolume ?vol .
    ?vol :eTextForInstance ?mw .
    ?mw skos:prefLabel ?volpl .
  } union {
    ?R_RES :instanceHasVolume ?vol .
    ?ut :eTextInVolume ?vol .
    ?ut ?utp ?uto .
  } union {
    ?R_RES :instanceHasVolume ?vol .
    ?ut :eTextInVolume ?vol .
    ?ut :eTextForInstance ?mw .
    ?mw skos:prefLabel ?utpl .
  }
}
