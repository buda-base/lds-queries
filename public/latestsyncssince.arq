#QueryScope=General
#QueryReturnType=Graph
#QueryResults=10 last synced image instances
#QueryParams=D_SINCE
#QueryUrl=/latestsyncssince?D_SINCE=2020-08-20T00:00:00Z

#param.D_SINCE.type=datetime
#param.D_SINCE.subtype=xsd:dateTime
#param.D_SINCE.desc=the past bound of the searched log entries

construct {
     ?ii skos:prefLabel ?l ;
         tmp:thumbnailIIIFService ?th ;
         tmp:datesync ?sdate .
     tmp:totalRes tmp:totalSyncs ?nbii .
  } where {
    {
      select (count(?iiAdm) as ?nbii)
        where {
          ?iiAdm tmp:lastSync ?sdate ;
                 adm:status bda:StatusReleased .
          FILTER(?sdate > ?D_SINCE)
        }
    } union {
      {
        select ?ii ?sdate
        where {
          ?iiAdm tmp:lastSync ?sdate ;
                 adm:status bda:StatusReleased .
          FILTER(?sdate > ?D_SINCE)
          ?iiAdm adm:adminAbout ?ii .
        } order by desc(?sdate) limit 10
      }
      ?ii bdo:instanceReproductionOf/skos:prefLabel ?l .
    } union {
      {
        select ?ii ?sdate
        where {
          ?iiAdm tmp:lastSync ?sdate ;
                 adm:status bda:StatusReleased .
          FILTER(?sdate > ?D_SINCE)
          ?iiAdm adm:adminAbout ?ii .
        } order by desc(?sdate) limit 10
      }
      ?ii tmp:thumbnailIIIFService ?th .
    }
}
