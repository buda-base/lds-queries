#QueryScope=ImageInstance
#QueryReturnType=Table
#QueryResults=list scan requests created during a time interval
#QueryParams=D_FROM,D_TO
#QueryUrl=/scanrequests_time?D_FROM=2015-04-01T00:00:00Z&D_TO=2015-05-01T00:00:00Z
#QueryLimit=5000

#param.D_FROM.type=datetime
#param.D_FROM.subtype=xsd:dateTime
#param.D_FROM.desc=the lower bound of the search

#param.D_TO.type=datetime
#param.D_TO.subtype=xsd:dateTime
#param.D_TO.desc=the past upper of the search

select distinct ?w (max(?titleu) as ?titlem) (count(distinct ?ig) as ?igc) ?userl (max(?date) as ?maxd) {
  {
    # scan request log entries
    ?le a adm:ScanRequested ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    FILTER(lang(?userl) = "en")
    ?adm adm:logEntry ?le ;
         adm:adminAbout ?ig .
    ?w :instanceHasVolume ?ig ;
       :instanceReproductionOf ?mw .
    ?mw skos:prefLabel ?title .
    BIND(IF(lang(?title) = "bo-x-ewts", f:ewtsToUnicode(?title), ?title) as ?titleu)
  } union {
    # W record creation
    ?w a :ImageInstance .
    ?adm adm:adminAbout ?w ;
         adm:logEntry ?le .
    ?le a adm:InitialDataCreation ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?w :instanceHasVolume ?ig ;
       :instanceReproductionOf ?mw .
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    ?mw skos:prefLabel ?title .
    BIND(IF(lang(?title) = "bo-x-ewts", f:ewtsToUnicode(?title), ?title) as ?titleu)
  } union {
    # tbrc.org scan requests
    ?le a adm:ScanRequestCreation ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    FILTER(lang(?userl) = "en")
    ?adm adm:logEntry ?le ;
         adm:adminAbout ?w .
    ?w :instanceReproductionOf ?mw ;
       :instanceHasVolume ?ig .
    ?mw skos:prefLabel ?title .
    BIND(IF(lang(?title) = "bo-x-ewts", f:ewtsToUnicode(?title), ?title) as ?titleu)
  }
} group by ?w ?userl
