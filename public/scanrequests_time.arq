#QueryScope=ImageInstance
#QueryReturnType=Table
#QueryResults=10 last synced image instances
#QueryParams=D_FROM,D_TO
#QueryUrl=/scanrequests_time?D_FROM=2015-04-01T00:00:00Z&D_TO=2015-05-01T00:00:00Z
#QueryLimit=5000

#param.D_FROM.type=datetime
#param.D_FROM.subtype=xsd:dateTime
#param.D_FROM.desc=the lower bound of the search

#param.D_TO.type=datetime
#param.D_TO.subtype=xsd:dateTime
#param.D_TO.desc=the past upper of the search

# ID
# title (Unicode)
# nb volumes
# who
# when

select distinct ?w (max(?title) as ?titlem) (count(?ig) as ?igc) ?userl (max(?date) as ?maxd) {
  {
    # scan request log entries
    ?le a adm:ScanRequested ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    FILTER(lang(?userl) = "en")
    ?adm adm:logEntry ?le ;
         adm:adminAbout ?ig .
    ?w :instanceHasVolume ?ig ;
       :instanceReproductionOf ?mw .
    ?mw skos:prefLabel ?title .
  } union {
    # W record creation
    ?w a :ImageInstance .
    ?adm adm:adminAbout ?w ;
         adm:logEntry ?le .
    ?le a adm:InitialDataCreation ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?w :instanceHasVolume ?ig ;
       :instanceReproductionOf ?mw .
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    ?mw skos:prefLabel ?title .
  } union {
    # tbrc.org scan requests
    ?le a adm:ScanRequestCreation ;
        adm:logDate ?date .
    FILTER(?date > ?D_FROM && ?date < ?D_TO)
    ?le adm:logWho ?user .
    ?user skos:prefLabel ?userl .
    FILTER(lang(?userl) = "en")
    ?adm adm:logEntry ?le ;
         adm:adminAbout ?w .
    ?w :instanceReproductionOf ?mw ;
       :instanceHasVolume ?ig .
    ?mw skos:prefLabel ?title .
  }
} group by ?w ?userl
